dnl autoconf script for Prestan

AC_INIT(Prestan, 0.1.0, grase@cs.ucsc.edu)

AC_PREREQ(2.50)

AC_CONFIG_SRCDIR(Prestan.in)
AC_CONFIG_HEADER(config.h)

AC_DEFINE([_GNU_SOURCE], 1, [Define to enable GNU extensions])

AC_PROG_CC
AC_SET_MAKE
AC_PROG_INSTALL

dnl List of tests
AC_SUBST([TESTS], ["Prestan"])

NEON_REQUIRE(0,24)
NEON_WITHOUT_ZLIB
NEON_WITHOUT_ACL
NEON_TEST
NEON_VPATH_BUNDLED(['$(top_srcdir)/libneon'], [libneon], [NEON_NORMAL_BUILD()
NEON_XML_PARSER([\$(top_builddir)/expat])
])

dnl Check for getopt_long
AC_CHECK_FUNC(getopt_long,,[AC_LIBOBJ(lib/getopt); AC_LIBOBJ(lib/getopt1)])

NEON_DEBUG
NEON_WARNINGS

CPPFLAGS="$CPPFLAGS -I\${top_builddir} -I\$(top_srcdir)/lib"

AC_CONFIG_FILES([Prestan], [chmod +x Prestan])
AC_CONFIG_FILES([Makefile libneon/Makefile])

AC_OUTPUT

AC_MSG_NOTICE([Configured to build AC_PACKAGE_STRING:

  Install prefix:  ${prefix}
  Compiler:        ${CC}
  neon library:    ${neon_library_message}
  XML parser:      ${neon_xml_parser_message}
  SSL library:     ${neon_ssl_message}
 
Now run 'make' to compile the test suite.])

# For VPATH builds
for d in libneon src; do
	test -d $d || mkdir $d
done
